# scons construct file
# see http://www.scons.org/
#

#
# set LINUX environment variables
#
import sys,os
sys.path.append('../')
import localSconsLib

include_path = localSconsLib.getOSIncPath()
library_path = localSconsLib.getOSLibPath()
unit_test_path = localSconsLib.getSubPath('UnitTest++.h',
                                          include_path,
                                          'UnitTest++')
if unit_test_path:
    include_path.append(unit_test_path)
#
# setting for this project
#
include_option = ['../include', '.']
library_option = ['../lib']
optlib = ['MTToolBox', 'ntl', 'UnitTest++']
env = Environment(CFLAGS=['-Wall', '-O3', '-std=c99',
                          '-Wmissing-prototypes', '-Wextra'],
                  CXXFLAGS=['-Wall', '-g', '-O3', '-Wextra',
                            '-D__STDC_CONSTANT_MACROS',
                            '-D__STDC_FORMAT_MACROS'])
env.Append(LIBPATH=library_path + library_option)
env.Append(CPPPATH=include_path + include_option)

common_files = ['tinymt32.c', 'tinymt64.c', 'main.cpp']
test1_files = ['test_period.cpp',
               'test_search.cpp',
               'test_equidist.cpp']
test2_files = ['test_temper.cpp',
               'test_search_temper.cpp',
               'test_linearity.cpp',
               'test_mt.cpp']
#
# Library check
#
conf = Configure(env)
if not conf.CheckLib('ntl'):
    print 'Did not find libntl.a or ntl.lib, exiting!'
    Exit(1)
#if (not conf.CheckLib('UnitTest++') and
#        (not libcheck('UnitTest++', library_path))) :
#    print 'Did not find UnitTest++ library.'
#    Exit(1)
if not unit_test_path:
    print 'Did not find UnitTest++ Header.'
    print 'Skipping unit test.'
    Exit(0)
if conf.CheckHeader('gf2x.h'):
    optlib.append('gf2x')
if conf.CheckLib('gmp'):
    optlib.append('gmp')
env = conf.Finish()

#
# target build
#
env.Program('test_rtiny', ['test_rtiny.cpp'], LIBS=optlib)
env.Program('test_rlittle', ['test_rlittle.cpp'], LIBS=optlib)
test1 = env.Program('test1',
                    test1_files + env.Object(common_files),
                    LIBS=optlib)
test2 = env.Program('test2',
                    test2_files + env.Object(common_files),
                    LIBS=optlib)
Command("test1.passed", test1, localSconsLib.runUnitTest)
Command("test2.passed", test2, localSconsLib.runUnitTest)
