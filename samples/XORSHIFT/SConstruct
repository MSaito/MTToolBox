# scons construct file
# see http://www.scons.org/
#

#
# set LINUX environment variables
#
import sys,os
sys.path.append('../../')
import localSconsLib

include_path = localSconsLib.getOSIncPath()
library_path = localSconsLib.getOSLibPath()

#
# setting for this project
#
include_option = ['../../include', '.']
library_option = ['../../lib']
optlib = ['MTToolBox', 'ntl']
env = Environment(CFLAGS=['-Wall', '-O3', '-std=c99',
                          '-Wmissing-prototypes', '-Wextra',
                          '-DMAIN'],
                  CXXFLAGS=['-Wall', '-g', '-O3', '-Wextra',
                            '-D__STDC_CONSTANT_MACROS',
                            '-D__STDC_FORMAT_MACROS'])
env.Append(LIBPATH=library_path + library_option)
env.Append(CPPPATH=include_path + include_option)
debug = ARGUMENTS.get('debug', 0)
if int(debug):
    env.Append(CXXFLAGS = '-g')
    env.Append(CXXFLAGS = '-DDEBUG')

#
# Library check
#
conf = Configure(env)
if not conf.CheckLib('ntl'):
    print 'Did not find libntl.a or ntl.lib, exiting!'
    Exit(1)
if conf.CheckLib('gf2x'):
    optlib.append('gf2x')
if conf.CheckLib('gmp'):
    optlib.append('gmp')
env = conf.Finish()

#
# target build
#
env.Program('xorshift-1', ['xorshift-1.cpp'], LIBS=optlib)
env.Program('xorshift-2', ['xorshift-2.cpp'], LIBS=optlib)
env.Program('xorshift-3', ['xorshift-3.cpp'], LIBS=optlib)
env.Program('xorshift-4', ['xorshift-4.cpp'], LIBS=optlib)
env.Program('xorshift-5', ['xorshift-5.cpp'], LIBS=optlib)
env.Program('xorshift128', ['xorshift128.c'], LIBS=optlib)

Clean('all', Glob('*.o'))
