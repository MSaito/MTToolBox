# scons construct file
# see http://www.scons.org/
#

#
# set LINUX environment variables
#
import sys,os
sys.path.append('../../')
import localSconsLib

include_path = localSconsLib.getOSIncPath()
library_path = localSconsLib.getOSLibPath()
include_path = localSconsLib.convIncPath(include_path)

#
# setting for this project
#
include_option = ['../../include', '.']
library_option = ['../../lib']
optlib = ['MTToolBox', 'ntl']
env = Environment(CFLAGS=['-Wall', '-O3', '-std=c99',
                          '-Wmissing-prototypes', '-Wextra'],
                  CXXFLAGS=['-Wall', '-g', '-O3', '-Wextra',
                            '-D__STDC_CONSTANT_MACROS',
                            '-D__STDC_FORMAT_MACROS'])
env.Append(LIBPATH=library_path + library_option)
env.Append(CPPPATH=include_path + include_option)
debug = ARGUMENTS.get('debug', 0)
if int(debug) == 1:
    env.Append(CXXFLAGS = '-g')
    env.Append(CXXFLAGS = '-DDEBUG')
    env.Append(CXXFLAGS = '-O0')
if int(debug) == 2:
    env.Append(CXXFLAGS = '-g')
    env.Append(CXXFLAGS = '-O0')
if int(debug) == 3:
    env.Append(CXXFLAGS = '-DDEBUG')

#
# Library check
#
conf = Configure(env)
if not conf.CheckLib('ntl'):
    print 'Did not find libntl.a or ntl.lib, exiting!'
    Exit(1)
if conf.CheckLib('gf2x'):
    optlib.append('gf2x')
if conf.CheckLib('gmp'):
    optlib.append('gmp')
env = conf.Finish()

#
# target build
#
env.Program('mt1', ['mt1.cpp'], LIBS=optlib)
env.Program('mt2', ['mt2.cpp'], LIBS=optlib)

Clean('all', Glob('*.o'))
